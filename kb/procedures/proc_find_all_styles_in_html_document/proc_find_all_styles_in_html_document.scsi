scp_program -> proc_find_all_styles_in_html_document    
	(*
	-> rrel_params: .proc_find_all_styles_in_html_document_params
		(*
		-> rrel_1: rrel_in: _start_tag;;
		-> rrel_2: rrel_in: _set_for_styles;;
		*);;

	-> rrel_operators: .proc_find_all_styles_in_html_document_operators
		(*
		-> rrel_init: .proc_find_all_styles_in_html_document_operator0A
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_scp_var: rrel_node: _set_for_class;;  

			=> nrel_goto: .proc_find_all_styles_in_html_document_operator0B;; 
			*);;

		-> .proc_find_all_styles_in_html_document_operator0B
			(*
			<- searchElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var:  _start_tag;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _unknown_style;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_style;;

			=> nrel_then: .proc_find_all_styles_in_html_document_operator0C;;
			=> nrel_else: .proc_find_all_styles_in_html_document_operator1A;;
			*);;

		-> .proc_find_all_styles_in_html_document_operator0C
			(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _set_for_styles;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _unknown_style;;

			=> nrel_goto: .proc_find_all_styles_in_html_document_operator1A;;
			*);;
		
		-> .proc_find_all_styles_in_html_document_operator1A
			(*
			<- searchElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _start_tag;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _unknown;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_tag_content;;

			=> nrel_then: .proc_find_all_styles_in_html_document_operator1B;; 
			=> nrel_else: .proc_find_all_styles_in_html_document_operator4C;; 
			*);;

		-> .proc_find_all_styles_in_html_document_operator1B
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: set;;
			-> rrel_2: rrel_assign: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _unknown;;  

			=> nrel_then: .proc_find_all_styles_in_html_document_operator2A;;   
			=> nrel_else: .proc_find_all_styles_in_html_document_operator1C;;
			*);;

		-> .proc_find_all_styles_in_html_document_operator1C
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: rrel_link: _sc_text;;  //проверь идентифкатор
			-> rrel_2: rrel_assign: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _unknown;;  

			=> nrel_then: .proc_find_all_styles_in_html_document_operator4C;;
			=> nrel_else: .proc_find_all_styles_in_html_document_operator1D;;
			*);;

		-> .proc_find_all_styles_in_html_document_operator1D
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: rrel_not_relation: _unknown_class;; 
			-> rrel_2: rrel_assign: rrel_scp_var: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _unknown;;  

			=> nrel_then: .proc_find_all_styles_in_html_document_operator3A;;  
			=> nrel_else: .proc_find_all_styles_in_html_document_operator4C;; 
			*);;


		-> .proc_find_all_styles_in_html_document_operator2A
			(*
			<- searchSetStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _unknown;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: rrel_node: _unknown_tag;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _set_for_tags;;

			=> nrel_then: .proc_find_all_styles_in_html_document_operator2B;;
			=> nrel_else: .proc_find_all_styles_in_html_document_operator4C;; 
			*);;

		-> .proc_find_all_styles_in_html_document_operator2B
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _set_for_tags;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_for_erase;;
			-> rrel_3: rrel_assign: rrel_scp_var: rrel_node: _new_unknown;;

			=> nrel_then: .proc_find_all_styles_in_html_document_operator2C;;
			=> nrel_else: .proc_find_all_styles_in_html_document_operator4C;;
			*);;

		-> .proc_find_all_styles_in_html_document_operator2C
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_for_erase;;

			=> nrel_goto: .proc_find_all_styles_in_html_document_operator4A;;
			*);;

		-> .proc_find_all_styles_in_html_document_operator3A
			(*
			<- call;;

			-> rrel_1: rrel_fixed: rrel_scp_const: proc_find_class;;    
			-> rrel_2: rrel_fixed: rrel_scp_const: .proc_find_all_styles_in_html_document_operator3A_params
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _unknown_class;;
				-> rrel_2: rrel_fixed: rrel_scp_const: concept_tag;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _set_for_class;;
				*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_find_all_styles_in_html_document_operator3B;;  
			*);;

		-> .proc_find_all_styles_in_html_document_operator3B
			(*
			<- waitReturn;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_find_all_styles_in_html_document_operator3C;;
			*);;

		-> .proc_find_all_styles_in_html_document_operator3C
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var:  _set_for_class;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _unknown_class;;

			=> nrel_then: .proc_find_all_styles_in_html_document_operator3D;;
			=> nrel_else: .proc_find_all_styles_in_html_document_operator4C;;
			*);;

		-> .proc_find_all_styles_in_html_document_operator3D
			(*
			<- searchElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var:  _unknown;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _unknown_style;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_style;;

			=> nrel_then: .proc_find_all_styles_in_html_document_operator3E;;
			=> nrel_else: .proc_find_all_styles_in_html_document_operator4C;;
			*);;

		-> .proc_find_all_styles_in_html_document_operator3E
			(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _set_for_styles;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _unknown_style;;

			=> nrel_goto: .proc_find_all_styles_in_html_document_operator3F;;
			*);;

		-> .proc_find_all_styles_in_html_document_operator3F
			(*
			<- searchElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var:  _unknown;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_common: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _new_unknown;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_tag_content;;

			=> nrel_then: .proc_find_all_styles_in_html_document_operator4A;;
			=> nrel_else: .proc_find_all_styles_in_html_document_operator4C;; 
			*);;

		-> .proc_find_all_styles_in_html_document_operator4A
			(*
			<- call;;

			-> rrel_1: rrel_fixed: rrel_scp_const: proc_find_all_styles_in_html_document;;
			-> rrel_2: rrel_fixed: rrel_scp_const: .proc_find_all_styles_in_html_document_operator4A_params
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _new_unknown;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _set_for_styles;;
				*);;
			-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_find_all_styles_in_html_document_operator4B;; 
			*);;

		-> .proc_find_all_styles_in_html_document_operator4B 
			(*
			<- waitReturn;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

			=> nrel_goto: .proc_find_all_styles_in_html_document_operator2B;; 
			*);;	

		-> .proc_find_all_styles_in_html_document_operator4C   
			(*
			<- eraseEl;;

			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _set_for_class;;

			=> nrel_goto: .proc_find_all_styles_in_html_document_operator_return;;
			*);;

		-> .proc_find_all_styles_in_html_document_operator_return
			(*
			<- return;;
			*);;
		*);;
	*);;
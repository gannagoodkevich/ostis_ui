agent_of_printing_relevant_command
=> nrel_main_idtf:
	[агентная scp-программа вывода наиболее релевантной команды для пользователя] (* <- lang_ru;; *);
	[agent of scp-program of printing relevant command for user] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_of_printing_relevant_command 
	(*
	-> rrel_params: .agent_of_printing_relevant_command_params
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
			*);;
	-> rrel_operators: .agent_of_printing_relevant_command_operators
		(*

		-> rrel_init: .agent_of_printing_relevant_command_operator1
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

			=> nrel_then: .agent_of_printing_relevant_command_operator2;;
			=> nrel_else: .agent_of_printing_relevant_command_return;;
			*);;

		-> .agent_of_printing_relevant_command_operator2
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: action_to_print_relevant_command;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_then: .agent_of_printing_relevant_command_operator3;;
			=> nrel_else: .agent_of_printing_relevant_command_return;;
			*);;
		
		-> .agent_of_printing_relevant_command_operator3
			(*
			<- searchElStr3;;
			
			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _person;;
			
			=> nrel_then: .agent_of_printing_relevant_command_operatorJ1;;
			=> nrel_else: .agent_of_printing_relevant_command_return;;
			*);;	

		-> .agent_of_printing_relevant_command_operatorJ1 (*
			<- searchElStr3;;
			
			-> rrel_1: rrel_fixed: rrel_scp_const: ui_user;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _person;;
			
			=> nrel_then: .agent_of_printing_relevant_command_operatorA1;;
			=> nrel_else: .agent_of_printing_relevant_command_operator_operator_call_proc_of_adding_validating_message;;
			*);;

		-> .agent_of_printing_relevant_command_operator_operator_call_proc_of_adding_validating_message  
            (* 
            <- call;; 
 
            -> rrel_1: rrel_fixed: rrel_scp_const: proc_of_adding_validating_message;;
            -> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_printing_relevant_command_operator_operator_call_proc_of_adding_validating_message_params  
                (* 
                -> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
                -> rrel_2: rrel_fixed: rrel_scp_const: [Wrong parameter! It must be user sign(element of ui_user)];; 
                *);; 
            -> rrel_3: rrel_assign: rrel_scp_var: _descr2;; 
 
            => nrel_goto: .agent_of_printing_relevant_command_operator_operator_waitreturn_proc_of_adding_validating_message;; 
            *);; 
 
        -> .agent_of_printing_relevant_command_operator_operator_waitreturn_proc_of_adding_validating_message 
            (* 
            <- waitReturn;; 
 
            -> rrel_1: rrel_fixed: rrel_scp_var: _descr2;; 
 
            => nrel_goto: .agent_of_printing_relevant_command_operator_operator_unsuccessfully_finished_question;; 
            *);;

        -> .agent_of_printing_relevant_command_operator_operator_unsuccessfully_finished_question 
            (* 
            <- genElStr3;; 
 
            -> rrel_1: rrel_fixed: rrel_scp_const: question_finished_unsuccessfully;; 
            -> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;; 
            -> rrel_3: rrel_fixed: rrel_scp_var: _quest;;
 
            => nrel_goto: .agent_of_printing_relevant_command_return;;  
            *);;

		-> .agent_of_printing_relevant_command_operatorA1 (*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_scp_var: rrel_node: rrel_struct: rrel_const: _answer;;

			=> nrel_goto: .agent_of_printing_relevant_command_operatorA2;;
			*);;

		-> .agent_of_printing_relevant_command_operatorA2 (*
			<- contAssign;;

			-> rrel_1: rrel_assign: rrel_scp_var: _max;;
			-> rrel_2: rrel_fixed: rrel_scp_const: [0];;

			=> nrel_goto: .agent_of_printing_relevant_command_operator4;;
			*);;

		-> .agent_of_printing_relevant_command_operator4 (*
			<- sys_search;;

			-> rrel_1: rrel_fixed: rrel_scp_const: print_relevant_command_template;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ... (*
				-> ... (*
					-> rrel_1: rrel_fixed: rrel_scp_const: _my_action_param;;
					-> rrel_2: rrel_assign: rrel_scp_var: rrel_set: _actions;;
					*);;
				*);;
			-> rrel_3: rrel_fixed: rrel_scp_const: ... (*
				-> ... (*
					-> rrel_1: rrel_fixed: rrel_scp_const: _user_param;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _person;;
					*);;
				*);;

			=> nrel_then: .agent_of_printing_relevant_command_operator5;;
			=> nrel_else: .agent_of_printing_relevant_command_operatorZ1;;
			*);;

		-> .agent_of_printing_relevant_command_operatorZ1 (*	
			<- call;; 
			
			-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_adding_general_action_comment;;
			-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_determining_frequency_of_initiation_of_every_command_call_proc_of_adding_general_action_comment_params (* 
					-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
					-> rrel_2: rrel_fixed: rrel_scp_const: [The search hasn't given any results];; 
				*);;		  	
			-> rrel_3: rrel_assign: rrel_scp_var: _descr2;;		  
		
			=> nrel_goto: .agent_of_printing_relevant_command_operatorZ2;;
		*);; 

		-> .agent_of_printing_relevant_command_operatorZ2 (* 
			<- waitReturn;; 
		
			-> rrel_1: rrel_fixed: rrel_scp_var: _descr2;; 
		
			=> nrel_goto: .agent_of_printing_relevant_command_return;; 
		*);;

		-> .agent_of_printing_relevant_command_operator5
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _actions;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _action;;

			=> nrel_then: .agent_of_printing_relevant_command_operator6;;
			=> nrel_else: .agent_of_printing_relevant_command_operatorE1;; 
			*);;

		-> .agent_of_printing_relevant_command_operator6 (*
			<- eraseSetStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _actions;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: rrel_erase: _arcs;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _action;;

			=> nrel_goto: .agent_of_printing_relevant_command_operator7;;
			*);;

		-> .agent_of_printing_relevant_command_operator7 (*
			<- sys_search;;

			-> rrel_1: rrel_fixed: rrel_scp_const: print_relevant_command_template;;
			-> rrel_2: rrel_fixed: rrel_scp_const: ... (*
				-> ... (*
					-> rrel_1: rrel_fixed: rrel_scp_const: _my_question;;
					-> rrel_2: rrel_assign: rrel_scp_var: rrel_set: _questions;;
					*);;
				-> ... (*
					-> rrel_1: rrel_fixed: rrel_scp_const: _my_ui_file;;
					-> rrel_2: rrel_assign: rrel_scp_var: _ui_file;;
					*);;
				*);;
			-> rrel_3: rrel_fixed: rrel_scp_const: ... (*
				-> ... (*
					-> rrel_1: rrel_fixed: rrel_scp_const: _my_action_param;;
					-> rrel_2: rrel_fixed: rrel_scp_var: _action;;
					*);;
				*);;

			=> nrel_then: .agent_of_printing_relevant_command_operator8;;
			=> nrel_else: .agent_of_printing_relevant_command_operator5;; 
			*);;		

		-> .agent_of_printing_relevant_command_operator8
			(*
			<- contAssign;; 

			-> rrel_1: rrel_assign: rrel_scp_var: _count;;
			-> rrel_2: rrel_fixed: rrel_scp_const: [0];;

			=> nrel_goto: .agent_of_printing_relevant_command_operator9;;
			*);;

		-> .agent_of_printing_relevant_command_operator9 (*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _questions;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _question;;

			=> nrel_then: .agent_of_printing_relevant_command_operator10;;
			=> nrel_else: .agent_of_printing_relevant_command_operator12;;
			*);;

		-> .agent_of_printing_relevant_command_operator10 (*
			<- eraseSetStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _questions;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: rrel_erase: _arcs;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _question;;

			=> nrel_goto: .agent_of_printing_relevant_command_operator11;;
			*);;

		-> .agent_of_printing_relevant_command_operator11
			(*
			<- contAdd;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _count;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _count;;
			-> rrel_3: rrel_fixed: rrel_scp_const: [1];;

			=> nrel_goto: .agent_of_printing_relevant_command_operator9;;
			*);;

		-> .agent_of_printing_relevant_command_operator12 (*
			<- ifGr;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _count;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _max;;

			=> nrel_then: .agent_of_printing_relevant_command_operator13;;
			=> nrel_else: .agent_of_printing_relevant_command_operator5;;
			*);;

		-> .agent_of_printing_relevant_command_operator13
			(*
			<- contAssign;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _max;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _count;;

			=> nrel_goto: .agent_of_printing_relevant_command_operator14;;
			*);;

		-> .agent_of_printing_relevant_command_operator14
			(*
			<- varAssign;;

			-> rrel_1: rrel_assign: rrel_scp_var: _max_ui_file;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _ui_file;;

			=> nrel_goto: .agent_of_printing_relevant_command_operator5;;
			*);;
		
		-> .agent_of_printing_relevant_command_operatorE1 (*
			<- ifEq;;
			
			-> rrel_1: rrel_fixed: rrel_scp_var: _max;;
			-> rrel_2: rrel_fixed: rrel_scp_const: [0];;
			
			=> nrel_then: .agent_of_printing_relevant_command_return;;
			=> nrel_else: .agent_of_printing_relevant_command_operatorE2;;
			*);;

		-> .agent_of_printing_relevant_command_operatorE2 (*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: relevant_command;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _max_ui_file;;

			=> nrel_goto: .agent_of_printing_relevant_command_operatorE3;;
			*);;

		-> .agent_of_printing_relevant_command_operatorE3 (*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _pos_arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _max_ui_file;;

			=> nrel_goto: .agent_of_printing_relevant_command_operatorE4;;
			*);;

		-> .agent_of_printing_relevant_command_operatorE4 (*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _pos_arc;;
			-> rrel_3: rrel_fixed: rrel_scp_const: relevant_command;;

			=> nrel_goto: .agent_of_printing_relevant_command_operatorE5;;
			*);;

		-> .agent_of_printing_relevant_command_operatorE5 (*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _pos_arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _arc;;

			=> nrel_goto: .agent_of_printing_relevant_command_operatorE6;;
			*);;

		-> .agent_of_printing_relevant_command_operatorE6 (*
			<- genElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_scp_var: rrel_common: rrel_const: _arc;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _answer;;
			-> rrel_4: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc2;;
			-> rrel_5: rrel_fixed: rrel_scp_const: nrel_answer;;

			=> nrel_goto: .agent_of_printing_relevant_command_return;;			
			*);;
				
		-> .agent_of_printing_relevant_command_return
			(*
			<- return;;
			*);;
		*);;
	*);;

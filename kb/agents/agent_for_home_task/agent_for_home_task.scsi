agent_for_home_task
=> nrel_main_idtf:
	[агентная scp-программа запуска домашнего задания] (* <- lang_ru;; *);
	[agent scp-program of running home task] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_for_home_task
	(*
	-> rrel_params: .agent_for_home_task_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	-> rrel_operators: .agent_for_home_task_operator_set 
		(*
		-> rrel_init: .agent_for_home_task_init1
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_for_home_task_init2;;
			*);;

		-> .agent_for_home_task_init2
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: action_for_home_task;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_then: .agent_for_home_task_init3;;
			=> nrel_else: .agent_for_home_task_return;;
			*);;

		-> .agent_for_home_task_init3
			(*
			<- searchElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _param1;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_1;;

			=> nrel_then: .agent_for_home_task_init4;;
			=> nrel_else: .agent_for_home_task_return;; 
			*);;

		-> .agent_for_home_task_init4
			(*
			<- searchElStr5;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _param2;;
			-> rrel_4: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc2;;
		  	-> rrel_5: rrel_fixed: rrel_scp_const: rrel_2;;

			=> nrel_then: .agent_for_home_task_call_proc;;
			=> nrel_else: .agent_for_home_task_return;; 
			*);;

		-> .agent_for_home_task_call_proc
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_for_home_task;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_for_home_task_proc_for_home_task_call_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _param1;;
				-> rrel_2: rrel_fixed: rrel_scp_var: _param2;;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr;;

		  	=> nrel_goto: .agent_for_home_task_call_proc_wr;;
			*);;

		-> .agent_for_home_task_call_proc_wr
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr;;

		  	=> nrel_goto: .agent_for_home_task_return;;
			*);;

		-> .agent_for_home_task_return
			(*
			<- return;;
			*);;
		*);;
	*);;
